import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'eclipse'
	id 'com.gradleup.shadow' version '9.0.0-beta4'
}

group = 'com.soarclient'
version = '1.0-SNAPSHOT'

project.ext.lwjglVersion = "3.3.4"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		def osArch = System.getProperty("os.arch")
		if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
			project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
		} else if  (osArch.startsWith("ppc")) {
			project.ext.lwjglNatives += "-ppc64le"
		} else if  (osArch.startsWith("riscv")) {
			project.ext.lwjglNatives += "-riscv64"
		}
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.contains("64")
			? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
			: "natives-windows-x86"
		break
}

repositories {
    maven { url 'https://www.jitpack.io' }
	maven { url "https://repo.viaversion.com" }
	maven {
        name = "Lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
    mavenCentral()
}

dependencies {

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl-nfd"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

	implementation 'it.unimi.dsi:fastutil:8.5.14'
	implementation 'org.joml:joml:1.10.8'
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
	implementation 'org.java-websocket:Java-WebSocket:1.5.7'

	implementation('net.raphimc:MinecraftAuth:4.1.1') {
		exclude group: "com.google.code.gson", module: "gson"
		exclude group: "org.slf4j", module: "slf4j-api"
    }

	implementation('net.raphimc:ViaLoader:3.0.5-SNAPSHOT') {
		exclude group: "com.google.guava", module: "guava"
		exclude group: "org.slf4j", module: "slf4j-api"
    }

	implementation 'com.viaversion:viaversion-common:5.1.2-SNAPSHOT'
	implementation 'com.viaversion:viarewind-common:4.0.3'
	implementation 'com.viaversion:viabackwards-common:5.1.2-SNAPSHOT'
	implementation 'net.raphimc:viaaprilfools-common:3.0.5-SNAPSHOT'
	implementation 'net.raphimc:ViaLegacy:3.0.6-SNAPSHOT'
	implementation 'io.github.humbleui:types:0.2.0'

	implementation fileTree('libs')
}

jar {
    manifest {
        attributes "Main-Class" : "com.soarclient.launch.Launch"
    }
}